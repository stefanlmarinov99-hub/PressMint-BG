<?xml version="1.0"?>
<!-- Take root corpus file and output sample in $outDir directory -->
<!-- Script retains first and last component file, and first and last $Range paragraphs in them -->
<xsl:stylesheet 
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:xi="http://www.w3.org/2001/XInclude"
  xmlns:xs="http://www.w3.org/2001/XMLSchema"
  xmlns:tei="http://www.tei-c.org/ns/1.0" 
  xmlns="http://www.tei-c.org/ns/1.0"
  xmlns:et="http://nl.ijs.si/et" 
  exclude-result-prefixes="#all"
  version="2.0">

  <xsl:import href="pressmint-lib.xsl"/>
  
  <!-- Output directory for samples -->
  <xsl:param name="outDir"/>
  
  <!-- Revision responsible agency  -->
  <xsl:param name="revRespPers">corpus2sample.xsl</xsl:param>

  <!-- How many TEI files to take -->
  <xsl:param name="Files">3</xsl:param>

  <!-- How many utterances to select from start and end of component files -->
  <xsl:param name="Range">2</xsl:param>

  <!-- URI of the Location of the GitHub project putatively containing the output files -->
  <!-- i.e. the value of tei:publicationStmt (tei:idno[@type='url'], pubPlace -->
  <xsl:param name="GitHub-project">https://github.com/clarin-eric/PressMint</xsl:param>
  
  <xsl:variable name="today" select="format-date(current-date(), '[Y0001]-[M01]-[D01]')"/>

  <xsl:key name="facs" match="tei:*" use="substring-after(@facs, '#')"/>
  
  <!-- Select URIs of XInclude components that will be in the sample -->
  <xsl:variable name="components">
    <xsl:variable name="n" select="count(/tei:teiCorpus/xi:include)"/>
    <xsl:choose>
      <!-- When too few files -->
      <xsl:when test="$n &lt; $Files">
        <xsl:message select="concat('INFO: from ', $n , ' files selecting all of them: ')"/>
        <xsl:for-each select="/tei:teiCorpus/xi:include">
          <xsl:message select="concat('INFO: selecting component file ', @href)"/>
          <xsl:copy-of select="."/>
        </xsl:for-each>
      </xsl:when>
      <xsl:otherwise>
        <xsl:message select="concat('INFO: from ', $n , ' component files selecting ~', $Files, ' files:')"/>
      <xsl:for-each select="/tei:teiCorpus/xi:include">
        <xsl:if test="(position()-1) mod floor($n div $Files) = floor($n div $Files) - 1">
          <xsl:message select="concat('INFO: selecting component file ', @href)"/>
          <xsl:copy-of select="."/>
        </xsl:if>
      </xsl:for-each>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:variable>

  <xsl:output method="xml" indent="yes" suppress-indentation="w pc"/>
  
  <xsl:template match="/">
    <!-- Output root file -->
    <xsl:variable name="inFile" select="replace(base-uri(), '.+/([^/]+$)', '$1')"/>
    <xsl:result-document href="{$outDir}/{$inFile}" method="xml">
      <xsl:apply-templates/>
    </xsl:result-document>
    <!-- Output component file samples -->
    <xsl:variable name="inDir" select="replace(base-uri(), '/[^/]+$', '')"/>
    <!-- process component files -->
    <xsl:for-each select="$components/xi:include">
      <!-- Get rid of subdirectories if in original -->
      <xsl:variable name="href" select="replace(@href, '.+/', '')"/>
      <xsl:result-document href="{$outDir}/{$href}" method="xml">
        <xsl:apply-templates mode="component" select="document(concat($inDir, '/', @href))"/>
      </xsl:result-document>
    </xsl:for-each>
    <!-- Output taxonomy files as raw, unparsed text copies -->
    <xsl:for-each select="//tei:teiHeader//xi:include">
      <xsl:variable name="href" select="replace(@href, '.+/', '')"/>
      <xsl:result-document href="{$outDir}/{$href}" method="text">
        <xsl:value-of select="unparsed-text(concat($inDir, '/', @href))"/>
      </xsl:result-document>
    </xsl:for-each>
  </xsl:template>
  
  <xsl:template match="tei:teiCorpus">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:apply-templates select="tei:teiHeader"/>
      <xsl:for-each select="$components/xi:include">
        <xi:include href="{@href}"/>
      </xsl:for-each>
    </xsl:copy>
  </xsl:template>

  <xsl:template mode="component" match="/">
    <xsl:apply-templates select="tei:TEI"/>
  </xsl:template>
  
  <xsl:template match="tei:TEI">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:apply-templates select="tei:teiHeader"/>
      <xsl:variable name="text">
        <xsl:apply-templates select="tei:text"/>
      </xsl:variable>
      <!-- <text> needed by <facsimile> so only used <surface> (and <zone>) elements are retained -->
      <xsl:apply-templates select="tei:facsimile">
        <xsl:with-param name="text" select="$text"/>
      </xsl:apply-templates>
      <xsl:copy-of select="$text"/>
    </xsl:copy>
  </xsl:template>

  <xsl:template match="tei:teiHeader">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:apply-templates/>
      <xsl:if test="not(./tei:revisionDesc)">
        <revisionDesc>
          <xsl:call-template name="revisionSample"/>
        </revisionDesc>
      </xsl:if>
    </xsl:copy>
  </xsl:template>

  <xsl:template match="tei:titleStmt/tei:title">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:value-of select="replace(., '( SAMPLE)?\]', ' SAMPLE]')"/>
    </xsl:copy>
  </xsl:template>
    
  <xsl:template match="tei:publicationStmt/tei:date">
    <xsl:copy>
      <xsl:attribute name="when" select="$today-iso"/>
      <xsl:value-of select="$today-iso"/>
    </xsl:copy>
  </xsl:template>
    
  <!-- This makes a "proper" sample, but is confusing for those that
       take the samples as a model of how to prepare their corpora
       The template overwrites tei:publicationStmt/tei:idno[@type='handle'] with
       publicationStmt/tei:idno[@type='url'] = $GitHub-project
       
  <xsl:template match="tei:publicationStmt/tei:pubPlace"/>
  <xsl:template match="tei:publicationStmt/tei:idno[@type='handle']">
    <idno type="URL">
      <xsl:value-of select="$GitHub-project"/>
    </idno>
    <pubPlace>
      <ref target="{$GitHub-project}">
        <xsl:value-of select="$GitHub-project"/>
      </ref>
    </pubPlace>
  </xsl:template>
  <xsl:template match="tei:sourceDesc">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <bibl>
        <title>Multilingual interoperable corpora of historical newspapers PressMint</title>
        <xsl:copy-of select="ancestor::tei:teiHeader//tei:publicationStmt/tei:idno[@type='handle']"/>
      </bibl>
      <xsl:apply-templates/>
    </xsl:copy>
  </xsl:template>
  -->
  
  <xsl:template match="tei:extent | tei:tagsDecl">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:comment>These numbers do not reflect the size of the sample!</xsl:comment>
      <xsl:apply-templates/>
    </xsl:copy>
  </xsl:template>
  
  <xsl:template match="tei:revisionDesc">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:call-template name="revisionSample"/>
      <xsl:apply-templates/>
    </xsl:copy>
  </xsl:template>

  <xsl:template name="revisionSample">
    <change when="{$today-iso}"><name><xsl:value-of select="$revRespPers"/></name>: Made sample.</change>
  </xsl:template>

  <xsl:template match="tei:facsimile">
    <xsl:param name="text"/>
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:apply-templates>
        <xsl:with-param name="text" select="$text"/>
      </xsl:apply-templates>
    </xsl:copy>
  </xsl:template>

  <!-- Select only surfaces and zones that are referred to from the sample -->
  <xsl:template match="tei:surface | tei:zone">
    <xsl:param name="text"/>
    <xsl:variable name="check">
      <xsl:apply-templates mode="check" select=".">
        <xsl:with-param name="text" select="$text"/>
      </xsl:apply-templates>
    </xsl:variable>
    <xsl:if test="normalize-space($check)">
      <xsl:copy>
        <xsl:message select="concat('INFO: selecting ', name(), ' ', @xml:id)"/>
        <xsl:apply-templates select="@*"/>
        <xsl:apply-templates>
          <xsl:with-param name="text" select="$text"/>
        </xsl:apply-templates>
      </xsl:copy>
    </xsl:if>
  </xsl:template>

  <!-- Return "OK" if element of any nested elements is referred to in $text -->
  <xsl:template mode="check" match="tei:surface | tei:zone">
    <xsl:param name="text"/>
    <xsl:choose>
      <xsl:when test="key('facs', @xml:id, $text)">OK</xsl:when>
      <xsl:otherwise>
        <xsl:apply-templates mode="check">
          <xsl:with-param name="text" select="$text"/>
        </xsl:apply-templates>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>
 
  <!-- Here we pick the first and last $Range paragraphs and all
       immediatelly preceding and intervening other elements -->
  <xsl:template match="tei:body">
    <xsl:variable name="all" select="count(//tei:text//tei:p)"/>
    <xsl:if test="not(.//tei:p)">
      <xsl:message select="concat('ERROR: no paragraphs in ', /tei:TEI/@xml:id)"/>
    </xsl:if>
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:variable name="to">
        <xsl:choose>
          <!-- If there is too few <p>s in the document -->
          <xsl:when test="$all &lt; $Range">
            <xsl:value-of select="generate-id((.//tei:p)[last()])"/>
          </xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="generate-id((.//tei:p)[position() = $Range])"/>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:variable>
      <xsl:variable name="from">
        <xsl:choose>
          <!-- If there is too few <p>s in the document -->
          <xsl:when test="$all &lt; 2 * $Range">0</xsl:when>
          <xsl:otherwise>
            <xsl:value-of select="generate-id((.//tei:p)[position() = $all - ($Range - 1)])"/>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:variable>
      <xsl:call-template name="process-text">
        <xsl:with-param name="from" select="$from"/>
        <xsl:with-param name="to" select="$to"/>
      </xsl:call-template>
    </xsl:copy>
  </xsl:template>
  
  <xsl:template name="process-text">
    <xsl:param name="from">0</xsl:param>
    <xsl:param name="to">0</xsl:param>
    <xsl:message select="concat('INFO: selecting sample from ', /tei:TEI/@xml:id, ': ', $to, ' and ', $from)"/>
    <xsl:variable name="text">
      <xsl:variable name="incipit">
        <xsl:apply-templates>
          <xsl:with-param name="to" select="$to"/>
        </xsl:apply-templates>
      </xsl:variable>
      <xsl:variable name="explicit">
        <xsl:apply-templates>
          <xsl:with-param name="from" select="$from"/>
        </xsl:apply-templates>
      </xsl:variable>
      <xsl:if test="$incipit/tei:*">
        <xsl:copy-of select="$incipit"/>
        <xsl:if test="$explicit/tei:*">
          <gap reason="editorial"><desc xml:lang="en">SAMPLING</desc></gap>
        </xsl:if>
      </xsl:if>
      <xsl:copy-of select="$explicit"/>
    </xsl:variable>
    <xsl:if test="$text//tei:p">
      <xsl:copy-of select="$text"/>
    </xsl:if>
  </xsl:template>
  
  <xsl:template match="tei:body/node()">
    <xsl:param name="from">0</xsl:param>
    <xsl:param name="to">0</xsl:param>
    <xsl:if test="($from = '0' and (self::tei:* | following::tei:* | .//tei:*)[generate-id(.) = $to]) or 
                  ($to   = '0' and (self::tei:* | preceding::tei:* | .//tei:*)[generate-id(.) = $from])">
      <xsl:choose>
        <xsl:when test="self::tei:gap[@reason='editorial' and ./tei:desc/text() = 'SAMPLING']" /> <!-- don't copy gap/desc SAMPLING -->
        <xsl:when test="self::tei:*">
          <xsl:copy>
            <xsl:apply-templates select="@*"/>
            <xsl:apply-templates/>
          </xsl:copy>
        </xsl:when>
        <xsl:when test="self::text()">
          <xsl:value-of select="."/>
        </xsl:when>
      </xsl:choose>
    </xsl:if>
  </xsl:template>

  <xsl:template match="tei:*">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:apply-templates/>
    </xsl:copy>
  </xsl:template>
  <xsl:template match="@*">
    <xsl:copy/>
  </xsl:template>
  <xsl:template match="xi:include[ancestor::tei:teiHeader]">
    <xsl:message select="concat('INFO: selecting meta file ', @href)"/>
    <xsl:copy-of select="."/>
  </xsl:template>

</xsl:stylesheet>
